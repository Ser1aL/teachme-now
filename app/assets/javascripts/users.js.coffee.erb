# Place all the behaviors and hooks related to the matching controller here.
# All this logic will automatically be available in application.js.
# You can use CoffeeScript in this file: http://jashkenas.github.com/coffee-script/

load_user_tabs = ->
  active_tab = $ ".main .profile .tabs .tab.active"
  if active_tab.length > 0
    $(".right .ajax_loader").removeClass "invisible"
    $(".profile .content").html ""
    url = active_tab.data().url
    $.ajax
      url: url
      type: 'get'
      success: (response) ->
        $(".profile .content").html response
        $(".right .ajax_loader").addClass "invisible"
        activate_subscription_links()
        activate_load_more_button()

activate_load_more_button = ->
  $(".connected_users_wrapper .user_connections_bottom .more_loadable").click ->
    button = $ this
    button.html button.data().loading
    $.ajax
      url: button.data().url
      data:
        page: button.data().page
      success: (response) ->
        $(".profile .right .content").html response
        activate_load_more_button()
        activate_subscription_links()



activate_subscription_links = ->
  $(".connected_user .info .management .subscribe, .connected_user .info .management .unsubscribe").click (event) ->
    event_handler = $ this
    url = event_handler.find("a")
    $.ajax
      url: url.attr("href")
      type: 'post'
      data:
        _method: url.data().method
        connection_type: url.data().connection_type
      success: (response) ->
        totals = url.closest(".connected_users_wrapper").find(".totals b")
        if url.data().connection_type == 'followers'
          totals.html(parseInt(totals.html()) - 1)
          url.closest(".connected_user").remove()
        else
          totals.html if event_handler.hasClass("subscribe") then parseInt(totals.html()) + 1 else parseInt(totals.html()) - 1
          url.closest(".management").find(".subscribe, .unsubscribe").removeClass "invisible"
          url.closest("div").addClass("invisible")
    false


$ ->
  $("#not_social_link").click (event) ->
    event.preventDefault()
    $(".sign_in .form_wrapper").slideToggle()

  $(".submittable").click (event) ->
    event.preventDefault()
    $(this).closest("form").submit()

  $(".clickable").click (event) ->
    event.preventDefault()
    document.location = $(this).closest("a").attr("href")

  $(".vote_up, .vote_down").click (event) ->
    element = $ this
    return if element.hasClass 'disabled'
    $.ajax
      url: $(this).data().url
      type: 'post'
      data:
        _method: $(this).data().method
      success: (response) ->
        $(".vote_up, .vote_down").removeClass "disabled"
        element.addClass "disabled"
        $(".rating .value").html response.rating

  load_user_tabs()

  $(".tabs .tab").click (event) ->
    $(".tabs .tab.active").removeClass "active"
    $(this).addClass "active"
    load_user_tabs()

  $(".profile .left .subscriptions .more_loadable").click ->
    $(".tabs #subscribers_tab").click()

  $(".profile .left .user_hover_hint .subscribe").click (event) ->
    event.preventDefault()
    url = $(this).find("a")
    $.ajax
      url: url.attr('href')
      type: 'post'
      success: (response) ->
        url.closest(".subscribe").slideUp()

  # Social networks initialization
  $.ajax
    url: "#{window.location.protocol}//connect.facebook.net/en_US/all.js"
    dataType: 'script'
    cache: true

  window.fbAsyncInit = ->
    FB.init(appId: '<%= APP_CONFIG['oauth']['facebook'][Rails.env]['app_id'] %>', cookie: true, xfbml: true, status: true)

    $('.facebook').click (e) ->
      e.preventDefault()
      FB.login ((response) ->
        window.location = '/users/auth/facebook/callback' if response.authResponse), scope: 'publish_stream,email,offline_access',

  VK.init(apiId: '<%= APP_CONFIG['oauth']['vkontakte'][Rails.env]['app_id'] %>')
  $('.vkontakte').click (e) ->
    e.preventDefault()
    VK.Auth.login ((response) ->
      window.location = '/vkontakte_transitional' if response.session), null, '/users/auth/vkontakte/callback'

  VK.Widgets.Like("vk_like", {type: "button"});

